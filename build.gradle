// Sadly, you can't use a gradle variable for a plugin version
// ref: https://stackoverflow.com/questions/37555196/in-gradle-how-to-use-a-variable-for-a-plugin-version
plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    // From developers of architectury: "shouldn't hardcode the loom version, just put *-SNAPSHOT"
    // ref: https://maven.architectury.dev/dev/architectury/architectury-loom/
    id "dev.architectury.loom" version "1.9-SNAPSHOT" apply false
    id 'com.palantir.git-version' version '3.0.0'
}

def git = versionDetails()
def tag = git.lastTag.substring(1)
if (System.getenv("GITHUB_ACTIONS") == "true") {
    if (System.getenv("GITHUB_WORKFLOW") == "Release") {
        version = tag
    } else if (System.getenv("PULL_REQUEST")) {
        version = "${tag}${tag.contains("-") ? "." : "-"}SNAPSHOT.pr.${System.getenv("PULL_REQUEST")}.${System.getenv("GITHUB_RUN_NUMBER")}"
    } else {
        version = "${tag}${tag.contains("-") ? "." : "-"}SNAPSHOT.${System.getenv("GITHUB_RUN_NUMBER")}"
    }
} else {
    version = "${tag}${tag.contains("-") ? "." : "-"}SNAPSHOT"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    version = "${rootProject.version}+${project.name}"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        // Minecraft 1.20.5 now requires Java 21 to run.
        // ref: https://fabricmc.net/2024/04/19/1205.html
        options.release = 21
    }

    java {
        withSourcesJar()
    }
}
