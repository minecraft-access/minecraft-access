plugins {
    alias(libs.plugins.shadow)
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    // Loom sets the log4j config while in development (in prod the log4j config might be dictated by the launcher or just the game itself).
    // This loom gradle config specifies that loom uses our customized log4j config file.
    // ref: https://fabricmc.net/wiki/documentation:fabric_loom
    // (although we're using the architectury fork of fabric loom, this config remains unchanged)
    log4jConfigs.from(file("../common/src/main/resources/log4j.xml"))
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

dependencies {
    neoForge libs.neoForge

    modImplementation include(libs.architecturyApiNeoForge.get().toString())

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/neoforge.mods.toml") {
        expand Map.of("version", project.version,
                "neoforge_version", libs.neoForge.get().version,
                "architectury_api_version", libs.architecturyApiNeoForge.get().version,
                "jade_neoforge_version", libs.jadeNeoForge.get().version,
                "minecraft_version", libs.minecraft.get().version)
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
